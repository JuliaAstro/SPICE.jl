export 
    rav2xf,
    reclat,
    recpgr,
    recrad,
    rotate

"""
    rav2xf(rot, av)

Determine a state transformation matrix from a rotation matrix and the angular
velocity of the rotation.

### Arguments ###

- `rot`: Rotation matrix
- `av`: Angular velocity vector

### Output ###

Returns state transformation matrix associated with `rot` and `av`.

### References ###

- [NAIF Documentation](https://naif.jpl.nasa.gov/pub/naif/toolkit_docs/C/cspice/rav2xf_c.html)
"""
function rav2xf(rot, av)
    xform = Array{SpiceDouble}(undef, 6, 6)
    ccall((:rav2xf_c, libcspice), Cvoid,
          (Ptr{SpiceDouble}, Ptr{SpiceDouble}, Ptr{SpiceDouble}),
          rot, av, xform)
    xform
end




"""
    reclat(rectan)

    Convert from rectangular coordinates to latitudinal coordinates.

### Arguments ###

- `rectan`: Rectangular coordinates of a point.

### Output ###

- `rad`: Distance of the point from the origin
- `lon`: Planetographic longitude of the point (radians).
- `lat`: Planetographic latitude of the point (radians).

### References ###

- [NAIF Documentation](https://naif.jpl.nasa.gov/pub/naif/toolkit_docs/C/cspice/reclat_c.html)
"""
function reclat(rectan)
    lon = Ref{Cdouble}()
    lat = Ref{Cdouble}()
    rad = Ref{Cdouble}()
    ccall((:reclat_c, libcspice), Cvoid, (Ptr{SpiceDouble}, Ref{Cdouble}, Ref{Cdouble}, Ref{Cdouble}), rectan, rad, lon, lat)
    return rad[], lon[], lat[]
end






"""
    recpgr(body, rectan, re, f)

    Convert rectangular coordinates to planetographic coordinates.

### Arguments ###

- `body`: Body with which coordinate system is associated
- `rectan`: Rectangular coordinates of a point
- `re`: Equatorial radius of the reference spheroid
- `f`: flattening coefficient

### Output ###

- `lon`: Planetographic longitude of the point (radians).
- `lat`: Planetographic latitude of the point (radians).
- `alt`: Altitude of the point above reference spheroid.

### References ###

- [NAIF Documentation](https://naif.jpl.nasa.gov/pub/naif/toolkit_docs/C/cspice/recpgr_c.html)
"""
function recpgr(body::AbstractString, rectan, re::Float64, f::Float64)
    lon = Ref{Cdouble}()
    lat = Ref{Cdouble}()
    alt = Ref{Cdouble}()
    ccall((:recpgr_c, libcspice), Cvoid, (Cstring, Ptr{SpiceDouble}, Cdouble, Cdouble, Ref{Cdouble}, Ref{Cdouble}, Ref{Cdouble}), body, rectan, re, f, lon, lat, alt)
    handleerror()
    return lon[], lat[], alt[]
end



"""
    rotate(angle, iaxis)

Calculate the 3x3 rotation matrix generated by a rotation 
of a specified angle about a specified axis. This rotation 
is thought of as rotating the coordinate system. 
 
### Arguments ###

- `angle`: Angle of rotation (radians)
- `iaxis`: Axis of rotation (X=1, Y=2, Z=3)

### Output ###

Returns rotation matrix associated with `angle` and `iaxis`.

### References ###

- [NAIF Documentation](https://naif.jpl.nasa.gov/pub/naif/toolkit_docs/C/cspice/rotate_c.html)
"""
function rotate(angle, iaxis)
    r = Matrix{SpiceDouble}(undef, 3, 3)
    ccall((:rotate_c, libcspice), Cvoid, (SpiceDouble, SpiceInt, Ptr{SpiceDouble}), angle, iaxis, r)
    permutedims(r)
end

"""
    recrad(rectan)

Convert rectangular coordinates to range, right ascension, and
declination.
 
### Arguments ###

- `rectan`: Rectangular coordinates of a point

### Output ###

Return the tuple `(range, ra, dec)`.

- `range`: Distance of the point from the origin
- `ra`: Right ascension in radians
- `dec`: Declination in radians

### References ###

- [NAIF Documentation](https://naif.jpl.nasa.gov/pub/naif/toolkit_docs/C/cspice/recrad_c.html)
"""
function recrad(rectan)
    range = Ref{SpiceDouble}()
    ra = Ref{SpiceDouble}()
    dec = Ref{SpiceDouble}()
    ccall((:recrad_c, libcspice), Cvoid, (Ptr{SpiceDouble}, Ref{SpiceDouble}, Ref{SpiceDouble}, Ref{SpiceDouble}), rectan, range, ra, dec)
    range[], ra[], dec[]
end
